import LftCM.Common
import Mathlib.CategoryTheory.Limits.FinallySmall
import Mathlib.CategoryTheory.Elements

open CategoryTheory Opposite

noncomputable section

universe v v' u u'

variable {C:Type u} [Category.{v} C]
variable {D:Type u'} [Category.{v'} D]
variable (F : C·µí·µñ √ó C ‚•§ D)
/- (Co)ends are a special type of limit for a bifunctor F: C·µí·µñ √ó C ‚Üí D.
If we think of F as a generalised bimodule, then the end ‚à´_cF is the 'center' - the subobject of Œ†F(c,c) of invariants by the action of F on arrows.
Simililarly, the coend ‚à´·∂úF is the 'space of coinvariants' - the quotient of ‚®øF(c,c) induced by the same action.

(Co)ends show up everywhere: regular (co)limits, many weighted limits, (pointwise) Kan extensions, ... In this light, (co)ends are an effective organisational tool.

Additionally, they admit a sort of calculus. For instance, we have a form of Fubini's rule which says ‚à´·∂ú‚à´·µâF‚âÖ‚à´·µâ‚à´·∂úF‚âÖ‚à´^(c√óe) F, for F:C·µí·µñ √ó C √ó E·µí·µñ √ó E ‚Üí D.

We define (co)ends via (co)wedges, which are most convenient to work with, and as special (co)limits, from which we derive the properties of (co)limits.
((It would also be nice to define (co)ends via weighted limits))
We also formalise theorems about (co)ends roughly corresponding to chapters 1-2 of 'Coend Calculus' by Fosco Loregian.

TODO:
* Define `IsEnd`, probably in terms if `IsLimit`
* Define `HasEnd`, probably in terms of `HasLimit`
* Make it easy to prove `IsEnd` by using the equivalence, so we can give a universal wedge directly
* Add a notion `end`, like we have `binaryProduct` etc., based on `limit` (limit cone by `choice`)
* Add notation for `end`
* Dualize everything
-/

-- **ends via the twisted arrow category**
abbrev TwistedArrow C [Category.{v} C] := (Functor.hom.{v, u} C).Elements

-- The induced functor Fbar : Tw(C) ‚Üí C·µí·µñ √ó C ‚Üí D corresponding to F : C·µí·µñ √ó C ‚Üí D
@[simp] def bar_fun : (TwistedArrow C) ‚•§ D := (CategoryOfElements.œÄ (Functor.hom C)) ‚ãô F

def endCone [Limits.HasLimit (bar_fun F)] := Limits.LimitCone (bar_fun F)

-- **ends via wedges**
def twisted_diagonal (F : C·µí·µñ √ó C ‚•§ D) : C ‚Üí D := fun c ‚Ü¶ F.obj (op c, c)

structure Wedge (F : C·µí·µñ √ó C ‚•§ D) where
  pt : D
  leg (c : C) : pt ‚ü∂ twisted_diagonal F c
  wedgeCondition ‚¶Éc c' : C‚¶Ñ (f : c ‚ü∂ c') :
    (leg c ‚â´ F.map ((ùüô c).op, f) : pt ‚ü∂ F.obj (op c, c'))
     = (leg c' ‚â´ F.map (f.op, ùüô c') : pt ‚ü∂ F.obj (op c, c'))
     := by aesop_cat

variable {F}

@[ext]
structure WedgeMorphism (x y : Wedge F) where
  hom : x.pt ‚ü∂ y.pt
  wedgeCondition : ‚àÄ c, hom ‚â´ y.leg c = x.leg c := by aesop_cat

attribute [simp] WedgeMorphism.wedgeCondition

@[simps]
def wedge_id (x : Wedge F) : WedgeMorphism x x where
  hom := ùüô x.pt

@[simps]
def wedge_comp {x y z : Wedge F} (f : WedgeMorphism x y) (g : WedgeMorphism y z) :
    WedgeMorphism x z where
  hom := f.hom ‚â´ g.hom

/-- The category of wedges on a functor `F`. -/
instance : Category (Wedge F) where
  Hom := fun x y => WedgeMorphism x y
  id := fun x => wedge_id x
  comp := fun f g => wedge_comp f g
open Limits

-- Definition of end via wedges
def myEnd [HasTerminal (Wedge F)] := terminal (Wedge F)

-- **Functor from Cone(Fbar) to Wd(F)**

-- Function on objects
@[simp]
def bar_F_cone_as_wedge ( c : Cone (bar_fun F)) : Wedge F where
  pt := c.pt
  leg (c' : C) := c.œÄ.app ‚ü®(op c', c'), ùüô c'‚ü©
  wedgeCondition d d' f := by
    have sq1 := c.w (j := ‚ü®(op d', d'), ùüô d'‚ü©) (j' := ‚ü®(op d, d'), f‚ü©) ‚ü®(f.op, ùüô _), by simp‚ü©
    have sq2 := c.w (j := ‚ü®(op d, d), ùüô d‚ü©) (j' := ‚ü®(op d, d'), f‚ü©) ‚ü®(ùüô _, f), by simp‚ü©
    aesop_cat

-- Function of morphisms
def bar_F_cone_mor_as_wedgeMorphism {c : Cone (bar_fun F)} {d : Cone (bar_fun F)}
    (f : ConeMorphism c d) : WedgeMorphism (bar_F_cone_as_wedge c) (bar_F_cone_as_wedge d) where
  hom := f.Hom

-- Functor
def functor_cone_to_wedge (F : C·µí·µñ √ó C ‚•§ D) : Functor (Cone (bar_fun F)) (Wedge F) where
  obj x := bar_F_cone_as_wedge x
  map f := bar_F_cone_mor_as_wedgeMorphism f

-- **Functor from Wd(F) to Cone(Fbar)**

-- Function of objects
@[simp]
def wedge_as_cone (w : Wedge F) : Cone (bar_fun F) where
  pt := w.pt
  œÄ := {
    app := fun g => (w.leg (unop g.1.1)) ‚â´ (F.map (ùüô g.1.1, g.2))
    naturality := by
      intro ‚ü®(d, d'), f‚ü© ‚ü®(e, e'), g‚ü© ‚ü®(h, h'), prop‚ü©
      simp only [Functor.hom_obj, Functor.const_obj_obj, bar_fun, Functor.comp_obj,
        CategoryOfElements.œÄ_obj, prod_Hom, Functor.hom_map, Functor.const_obj_map, op_unop,
        Category.id_comp, Functor.comp_map, CategoryOfElements.œÄ_map, Category.assoc] at prop ‚ä¢
      have sq1' := (reassoc_of% w.wedgeCondition h.unop) (Z := F.obj (e, e')) (F.map (ùüô e, f ‚â´ h'))
      rw [‚Üê F.map_comp, prod_comp] at sq1'
      simp only [op_unop, op_id, Category.comp_id, prop] at sq1'
      rw [sq1', ‚Üê F.map_comp, ‚Üê F.map_comp]
      simp
  }

--  Function on morphisms
@[simp]
def wedgeMorphism_as_coneMorphism {c : Wedge F} {d : Wedge F} (f : WedgeMorphism c d) :
    ConeMorphism (wedge_as_cone c) (wedge_as_cone d) where
  Hom := f.hom
  w := by
    intro ‚ü®(d, d'), Y‚ü©
    simp only [bar_fun, wedge_as_cone, Functor.const_obj_obj, Functor.hom_obj, Functor.comp_obj,
      CategoryOfElements.œÄ_obj, op_unop]
    rw [‚Üê f.wedgeCondition d.unop, Category.assoc]

-- Functor
@[simp]
def functor_wedge_to_cone (F : C·µí·µñ √ó C ‚•§ D) : Functor (Wedge F) (Cone (bar_fun F)) where
  obj x := wedge_as_cone x
  map f := wedgeMorphism_as_coneMorphism f

-- Equivalence of categories of Wd(F) and Cone(F bar)
def equivalence_cone_Fbar_WdF : Equivalence (Wedge F) (Cone (bar_fun F)) :=
  CategoryTheory.Equivalence.mk (functor_wedge_to_cone F) (functor_cone_to_wedge F)
    sorry
    sorry
